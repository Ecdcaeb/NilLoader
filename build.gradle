plugins {
	id 'com.github.ben-manes.versions' version '0.51.0'
	id 'com.gradleup.shadow' version '8.3.5'
	id 'java-library'
	id 'maven-publish'
}

sourceCompatibility = 8
targetCompatibility = 8

base {
	group = "com.unascribed"
	archivesName = "NilLoader"
	version = "1.3.5"
}

tasks.withType(JavaCompile) {
	options.release = 8
}

repositories {
	mavenCentral()
}

configurations {
	shade
	compileOnly.extendsFrom shade
}

dependencies {
	shade 'org.ow2.asm:asm:9.7.1'
	shade 'org.ow2.asm:asm-tree:9.7.1'
	shade 'org.ow2.asm:asm-analysis:9.7.1'
	shade 'org.ow2.asm:asm-commons:9.7.1'
	
	shade 'org.cadixdev:lorenz:0.5.8'
	shade 'org.cadixdev:lorenz-asm:0.5.8'

	shade 'com.grack:nanojson:1.9'
	
	compileOnly 'org.vineflower:vineflower:1.10.1'
	compileOnly 'org.slf4j:slf4j-api:1.7.36'
	compileOnly 'org.apache.logging.log4j:log4j-api:2.24.3'
	compileOnly 'log4j:log4j:1.2.17'
	compileOnly 'commons-logging:commons-logging:1.3.4'
}

processResources {
	inputs.property "version", version
	
	filesMatching("nilloader.nilmod.css") {
		expand "version": version
	}
}

jar {
	archiveClassifier = 'slim'
}

shadowJar {
	archiveClassifier = ''
	
	configurations = [project.configurations.shade]
	
	relocate 'org.objectweb.asm', 'nilloader.api.lib.asm'
	relocate 'com.grack.nanojson', 'nilloader.api.lib.nanojson'
	relocate 'org.cadixdev.lorenz', 'nilloader.impl.lib.lorenz'
	relocate 'org.cadixdev.bombe', 'nilloader.impl.lib.bombe'
	
	exclude 'META-INF/**'
	exclude 'LICENSE.txt'
	
	manifest {
		attributes (
			'Premain-Class': 'nilloader.NilAgent',
			'Agent-Class': 'nilloader.NilAgent',
			'Can-Retransform-Classes': 'true'
		)
	}
}

task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	
	from sourceSets.main.allSource
	from 'src/lib/java'
}

task prismJson(type: Copy) {
	from('prism.json') {
		expand 'version': project.version
	}

	into 'build/libs'
}

task popShadeSources {
	doFirst {
		// https://stackoverflow.com/a/55846906
		def toSourceDependency = { dep ->
			dependencies.create([
				group     : dep.module.id.group,
				name      : dep.module.id.name,
				version   : dep.module.id.version,
				classifier: 'sources',
				transitive: false
			])
		}
	
		configurations.shade.resolvedConfiguration.lenientConfiguration.allModuleDependencies.each { dep ->
			configurations
				.detachedConfiguration([toSourceDependency(dep)] as Dependency[])
				.resolvedConfiguration
				.lenientConfiguration
				.getFiles(Specs.SATISFIES_ALL)
				.each { f ->
					copy {
						from zipTree(f)
						exclude 'META-INF/**'
						into 'src/lib/java'
					}
				}
		}
	}
}

publishing {
	repositories {
		if (project.hasProperty("publish-username")) {
			maven {
				url "https://repo-api.sleeping.town/"
				credentials {
					username project.hasProperty("publish-username") ? project.getProperty("publish-username") : null
					password project.hasProperty("publish-password") ? project.getProperty("publish-password") : null
				}
			}
		}
		maven {
			url file('build/maven').toURI().toString()
		}
	}
	publications {
		maven(MavenPublication) {
			groupId = 'com.unascribed'
			artifactId = 'nilloader'
			version = project.version
			artifact shadowJar
			artifact sourcesJar
			artifact('build/libs/prism.json') {
				classifier 'prism'
			}
		}
	}
}


tasks.named("dependencyUpdates").configure {
	gradleReleaseChannel = 'current'
	revision = 'release'
	rejectVersionIf {
		it.candidate.version.contains("alpha") || it.candidate.version.contains("beta")
			|| (it.candidate.group == 'org.slf4j' && it.candidate.version.startsWith("2."))
	}
}


tasks.withType(AbstractPublishToMaven).each {
	it.dependsOn tasks.prismJson
}

tasks.build.dependsOn tasks.prismJson
tasks.build.dependsOn tasks.shadowJar
tasks.build.dependsOn tasks.sourcesJar
